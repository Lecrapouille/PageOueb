dnl -*- m4 -*- Coloration Syntaxique pour Emacs
dnl #################################################################
dnl # Useful macros
dnl #################################################################
define([CONCAT],[$1$2])dnl
define([UPPER],[translit([$1],[a-z],[A-Z])])dnl
define([LOWER],[translit([$1],[A-Z],[a-z])])dnl
define([forloop], [pushdef([$1], [$2])_forloop([$1], [$2], [$3], [$4])popdef([$1])])dnl
define([_forloop],[$4[]ifelse($1, [$3], ,[define([$1], incr($1))_forloop([$1], [$2], [$3], [$4])])])dnl
dnl #################################################################
dnl Rename a macro and forget the older macro name
dnl rename([define],[create])
dnl create([vehicle],[truck])
dnl vehicle                   # -> truck
dnl define([fuel],[diesel])   # -> define(fuel,diesel)
dnl fuel                      # -> fuel
define([rename], [define([$2],defn([$1]))undefine([$1])])dnl
dnl #################################################################
dnl Alias a macro name
define([alias], [define([$1],defn([$2]))])dnl
dnl #################################################################
dnl # Array
dnl Example: http://mbreen.com/m4.html
dnl  _set([myarray], 1, [alpha])
dnl  _get([myarray], 1)                  # -> alpha
dnl  _set([myarray], [alpha], [omega])
dnl  _get([myarray], _get([myarray],1))  # -> omega
dnl  defn([myarray[alpha]])              # -> omega
dnl #################################################################
define([_set],[define([$1{$2}],[$3])])dnl
define([_get],[defn([$1{$2}])])dnl
dnl #################################################################
dnl Dates
define([EN_DATE],esyscmd(sh -c "date '+%a %d %b %y' | tr -d '\n'"))dnl Date without its '\n'
dnl define([FR_DATE],esyscmd(sh -c "date | cut -d' ' -f1-5 | tr -d '\n'"))dnl
alias([FR_DATE],[EN_DATE])
