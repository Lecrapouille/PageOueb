dnl -*- m4 -*- Coloration Syntaxique pour Emacs
dnl #####################################################################
dnl HTML Meta and title
dnl #####################################################################
define([KEYWORDS],[define([_META_KEYWORDS_],[$1])])dnl
define([DESCRIPTION],[define([_META_DESCRIPTION_],[$1])])dnl
define([TITLE],[define([_META_TITLE_],[$1])])dnl
dnl #####################################################################
dnl HTML header and footer
dnl #####################################################################
define([HTML_FOOTER],[</body> </html>])dnl
define([HTML_HEADER],[<?xml version="1.0" encoding="iso-8859-1"?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="]LANG[" lang="]LANG[">
<head>

<!-- This page was generated by script DATE -->

<title>]_META_TITLE_[</title>
<meta name="keywords" content="]_META_KEYWORDS_[" />
<meta name="description" content="]_META_DESCRIPTION_[" />
<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
<link href=ifelse($1,,["css/styles.css"],["css/$1"]) rel="stylesheet" type="text/css" />
</head>
<body><a id="gotoup" name="gotoup"></a>

])dnl
dnl #####################################################################
dnl HTML validator
dnl #####################################################################
define([PHPMYVISITE],[<!-- phpmyvisites -->
<a href="http://st.free.fr/" title="phpMyVisites | Open source web analytics"
onclick="window.open(this.href);return(false);"><script type="text/javascript">
<!--
var a_vars = Array();
var pagename='';

var phpmyvisitesSite = 21477;
var phpmyvisitesURL = "http://st.free.fr/phpmyvisites.php";
//-->
</script>
<script language="javascript" src="http://st.free.fr/phpmyvisites.js" type="text/javascript"></script>
<object><noscript>
<img src="img/phpmyvisites.png" alt="Statistics" style="border:0" />
</noscript></object></a>
<!-- /phpmyvisites -->])dnl
dnl #####################################################################
dnl HTML validator
dnl #####################################################################
define([CSS_VALIDATOR],[<a href="http://jigsaw.w3.org/css-validator/check/referer">
<img src="img/css.gif" alt="Valid CSS!" style="border:0" height="15" width="66" /></a>])dnl
define([XHTML_VALIDATOR],[<a href="http://validator.w3.org/check?uri=referer">
<img src="http://www.w3.org/Icons/valid-xhtml10" alt="Valid XHTML 1.0!"
style="border:0" height="15" width="66" /></a>])dnl
dnl #####################################################################
dnl HTML Layout
dnl #####################################################################
define([STRONG],[<strong>$1</strong>])dnl
define([BOLD],[STRONG])dnl
define([MATHS],[<strong>$1</strong>])dnl
define([ITALIC],[<em>$1</em>])dnl
define([REMARK],[<strong>Remarque :</strong>])dnl
define([CENTER],[<div class="centrer">$@</div>])dnl
define([RIGHT],[<div class="right">$@</div>])dnl
define([HUDGE],[<div class="hudge">$@</div>])dnl
dnl #####################################################################
dnl HTML color
dnl #####################################################################
define([COLOR_],[<font color="$1"><strong>$2</strong></font>])dnl
define([COLOR],[<font color="#$1"><strong>$2</strong></font>])dnl
define([BLACK],[COLOR_(black,$1)])dnl
define([BLUE],[COLOR_(2100ae,$1)])dnl
define([PINK],[COLOR_(fuchsia,$1)])dnl
define([GREY],[COLOR_(grey,$1)])dnl
define([GREEN],[COLOR_(green,$1)])dnl
define([MAROON],[COLOR_(maroon,$1)])dnl
define([BROWN],[COLOR_(maroon,$1)])dnl
define([YELLOW],[COLOR_(yellow,$1)])dnl
define([ORANGE],[COLOR_(ff9900,$1)])dnl
define([WHITE],[COLOR_(white,$1)])dnl
define([RED],[COLOR_(red,$1)])dnl
dnl #####################################################################
dnl HTML displaying programming language
dnl #####################################################################
define([CODE],[define([CODE_],[$1])][<div class="code"><pre><code>])dnl
define([ENDCODE],[</code></pre></div>define([CODE_],)])dnl
dnl #####################################################################
dnl HTML box
dnl #####################################################################
define([BOX],[<div class="boite">])dnl
define([ENDBOX],[RIGHT(LINK(#gotoup,ICON_ARROW_UP)) </div>])dnl
dnl ---------------------------------------------------------------------
dnl FIXME should be width=100% not 90%
define([FOOTER_BOX],[TABLE(4,
<h3>DISP_PREV</h3>,
<h3>DISP_HOME</h3>,
<h3>DISP_UP</h3>,
<h3>DISP_NEXT</h3>,
ifelse([$1],,[ICON_ARROW_LEFT],[ICON_ARROW_LLEFT]),
LINK(index-LANG.html,ICON_ARROW_HOME),
LINK(#gotoup,ICON_ARROW_UP),
ifelse([$3],,[ICON_ARROW_RIGHT],[ICON_ARROW_RRIGHT]),
ifelse([$1],,,[MYLINK($1,$2)]),
,,
ifelse([$3],,,[MYLINK($3,$4)]))
])dnl
dnl #####################################################################
dnl HTML links
dnl #####################################################################
dnl Generic external link
define([LINK],[ifelse([$2],,[<a href="$1">$1</a>],[<a href="$1">$2</a>])])dnl
dnl Generic internal link
define([ILINK],[ifelse([$2],,[<a href="#$1">$1</a>],[<a href="#$1">$2</a>])])dnl
dnl ---------------------------------------------------------------------
dnl Local link (refering to a page of my website)
define([MYLINK],[ifelse([$2],,[<a href="$1-LANG.html">$1</a>],[<a href="$1-LANG.html">$2</a>])])dnl
dnl ---------------------------------------------------------------------
dnl Non local link (refering to a page outside my website)
define([EXTLINK],[ICON_EXT_LINK<a href="$1" target="_blank">ifelse([$2],,[$1],[$2])</a>])dnl
dnl ---------------------------------------------------------------------
dnl GitHub link (https://github.com/ is already set)
define([GITHUB],[ICON_GITHUB<a href="https://github.com/$1" target="_blank">ifelse([$2],,[https://github.com/$1],[$2])</a>])dnl
dnl #####################################################################
dnl Link to pictures
dnl #####################################################################
define([MOVIE],[<a href="$2"><img class="caption" src="$1" alt="caption"/></a>])dnl
define([CAPTION],[<a href="$1"><img class="caption" src="$1" alt="caption"/></a>])dnl
define([LOWCAPTION],[<a href="$1_big.jpg"><img class="caption" src="$1.jpg" alt="caption"/></a>])dnl
define([PICTURE],[CENTER(CAPTION($1)<p>$2</p>)])dnl
define([LOWPICTURE],[CENTER(LOWCAPTION($1)<p>$2</p>)])dnl
dnl #####################################################################
dnl HTML table
dnl #####################################################################
define([head_],[$1[]define([_list],[shift($@)])])dnl
dnl ---------------------------------------------------------------------
define([TABLE],[define([_col_],[$1])dnl Number of columns
define([_lig_],[eval($# / _col_)])dnl Number of lines
define([_list],[shift($@)])
<table width="90%" align="center">
forloop(iii,1,_lig_,[    <tr>
forloop(jjj,1,_col_,[        <td align="center">head_(_list)</td>
])    </tr>
])
</table>])dnl
dnl #####################################################################
dnl HTML List and enum
dnl #####################################################################
define([recursive_list_],[ifelse([$1],,,[<li>$1</li> recursive_list_(shift($@))])])dnl
define([LIST],[<!-- Begin list -->
<ul>recursive_list_($@)</ul>
<!-- End list -->])dnl
dnl ---------------------------------------------------------------------
define([ENUM],[<!-- Begin enum -->
<ol>recursive_list_($@)</ol>
<!-- End enum -->])dnl
dnl #####################################################################
dnl HTML Table of contents (made through sections)
dnl #####################################################################
define([list_toc],[])
define([add_toc],[define([list_toc],list_toc
$@)])

define([DIVERT_TABLE_OF_CONTENT],[99])dnl
define([display_table_of_contents_],[ifelse($1,,,[<li>ILINK($2,$1)</li>
display_table_of_contents_(shift(shift($@)))])])
dnl ---------------------------------------------------------------------
define([TABLE_OF_CONTENTS],[<!-- Table of contents -->
<p>STRONG(ifelse(LANG,fr,[Table des matières :],[Table of contents:]))
</p><ul>list_toc</ul>])
dnl </p><ul>display_table_of_contents_($@)</ul>])
dnl #####################################################################
dnl HTML SECTION
dnl #####################################################################
dnl ---------------------------------------------------------------------
dnl Private macros
dnl ---------------------------------------------------------------------
dnl _def_section_
dnl $1: h1 h2 h3
dnl $2: icon
dnl $3: title
dnl $4: anchor
dnl $5: link
define([_html_section_],[
dnl FIXME: faire une pile
divert(DIVERT_TABLE_OF_CONTENT)dnl FIXME: divert_push(DIVERT_TABLE_OF_CONTENT)
add_toc(<li>ILINK($4,$3)</li>)
divert(DIVERT_BODY_CODE)dnl FIXME: divert_pop() et
dnl END FIXME
ifelse($5,,[ifelse($4,,[<$1>$2$3</$1>],[<a id="$4" name="$4"></a><$1>$2$3</$1>])],
[ifelse($4,,[<$1><a href="$5-]LANG[.html">$2$3</a></$1>],dnl
[<a id="$4" name="$4"></a><$1><a href="$5-]LANG[.html">$2$3</a></$1>])])])dnl
dnl ---------------------------------------------------------------------
dnl Section state: if 0 do not add ENDBOX, else add ENDBOX
define([_reset_state_section_],[define([_state_section_],[0])])dnl
define([_start_section_],[BOX define([_state_section_],[1])])dnl
define([_close_section_],[ifelse(_state_section_,0,,[ENDBOX])])dnl
_reset_state_section_
dnl ---------------------------------------------------------------------
define([_subsection_],[_close_section_
<!-- Subsection -->
_html_section_($@)
_start_section_])
dnl ---------------------------------------------------------------------
define([_section_],[_close_section_
<!-- Section -->
_html_section_($@)
_reset_state_section_])
dnl ---------------------------------------------------------------------
dnl Public macros
dnl ---------------------------------------------------------------------
define([SECTION],[_section_(h1,$@)])dnl
define([SUBSECTION],[_subsection_(h2,$@)])dnl
define([SUBSUBSECTION],[_subsection_(h3,$@)])dnl
